# Contributor: Nick Lanham (nick AT afternight DOT org)
# Contributor: Yejun Yang (yejunx AT gmail DOT com)

pkgname=ec2-api-tools
pkgver=1.5.6.0
pkgrel=1
pkgdesc="The API tools that serve as the client interface to the Amazon EC2 web service"
arch=('any')
url="http://aws.amazon.com/developertools/351"
source=(http://ec2-downloads.s3.amazonaws.com/$pkgname-$pkgver.zip)
depends=(java-runtime)
license=('custom')
md5sums=('c6a3abe4bc4b1a31f29890404f93150a')
install=$pkgname.install
_installDir=/opt/ec2-api-tools
build() {
	cd $srcdir/$pkgname-$pkgver
	mkdir -p $pkgdir$_installDir
	# install lib i.e jar file
	cp -r lib $pkgdir$_installDir
	find $pkgdir -type f -exec chmod 644 {} \;

	# fix env. var. in scripts
	sed -i 's#:?EC2_HOME is not set#:=/opt/ec2-api-tools#' bin/*
	sed -i -e 's#:-EC2_HOME is not set#:=/opt/ec2-api-tools#' \
		-e 's#:?JAVA_HOME is not set#:-/opt/java#' bin/ec2-cmd
	# and install scripts
	cp -r bin $pkgdir$_installDir
	# Remove unneeded windows cmd files
	rm $pkgdir$_installDir/bin/*.cmd

	# install license files
	mkdir -p $pkgdir/usr/share/licenses/$pkgname/
	install -m 644 license.txt $pkgdir/usr/share/licenses/$pkgname/
	install -m 644 THIRDPARTYLICENSE.TXT $pkgdir/usr/share/licenses/$pkgname/
}

